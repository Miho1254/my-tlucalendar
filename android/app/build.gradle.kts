// Copyright (C) 2025 - 2026 Nguyen Duy Thanh (@Nekkochan0x0007). All right reserved

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // Cái dòng id(...) id(...) ở dưới kệ nó.
        // Thêm dòng này để dạy khôn thằng R8:
        classpath("org.jetbrains.kotlin:kotlin-metadata-jvm:2.3.21") 
        // Lưu ý: Nếu có bản 2.3.0 trên Maven thì để 2.3.0, 
        // còn không thì cứ để bản mới nhất mày tìm thấy. 
        // Thường bản 2.0+ hoặc 2.1+ là nó support backward/forward compatibility tốt hơn bản cũ rích của AGP.
    }
}

plugins {
    id("com.mikepenz.aboutlibraries.plugin")
    id("com.mikepenz.aboutlibraries.plugin.android")
    id("com.android.application")
    id("kotlin-android")
    id("com.google.gms.google-services")
    id("com.google.firebase.crashlytics")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
    // Compose Compiler plugin (required for Kotlin 2.0+)
    id("org.jetbrains.kotlin.plugin.compose")
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
    }
}

android {
    namespace = "com.nekkochan.tlucalendar"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        // Flag to enable support for the new language APIs
        isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.nekkochan.tlucalendar"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        // Version code with last is 20 is RELEASE CHANNEL
        // Version code with last is 30 is BETA CHANNEL
        // Version code with last is 40 is ALPHA CHANNEL
        // 21, 2x is INCREMENT VERSION for RELEASE CHANNEL
        versionCode = 2026032320
        versionName = flutter.versionName

        ndk {
            abiFilters += listOf("armeabi-v7a", "arm64-v8a")
        }

        externalNativeBuild {
            cmake {
                cppFlags += "-std=c++20"
                cFlags += "-O2"
                arguments += "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }

    // Signing configuration for release builds
    val keystoreFile = rootProject.file("rel.jks")

    if (keystoreFile.exists()) {
        signingConfigs {
            create("release") {
                storeFile = keystoreFile
                storePassword = System.getenv("STORE_PASSWORD") ?: ""
                keyAlias = System.getenv("KEY_ALIAS") ?: ""
                keyPassword = System.getenv("KEY_PASSWORD") ?: ""

                enableV3Signing = true
                enableV4Signing = true
            }
        }
    }

    buildTypes {
        debug {
            isMinifyEnabled = false
            isShrinkResources = false

            ndk {
                abiFilters += listOf("armeabi-v7a", "arm64-v8a")
            }
        }

        release {
            // Assign signing config if keystore exists
            if (keystoreFile.exists()) {
                signingConfig = signingConfigs.getByName("release")
            }
            
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )

            ndk {
                abiFilters += listOf("armeabi-v7a", "arm64-v8a")
            }
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.5")

    implementation("androidx.activity:activity-compose:1.13.0")
    implementation("androidx.compose.ui:ui:1.11.1")
    implementation("androidx.compose.material3:material3:1.4.0")
    implementation("androidx.compose.material:material-icons-extended:1.7.8")
    implementation("androidx.compose.material:material-icons-core:1.7.8")
    implementation("androidx.compose.ui:ui-tooling-preview:1.11.1")
    debugImplementation("androidx.compose.ui:ui-tooling:1.11.1")

    implementation("androidx.core:core-ktx:1.18.0")
    implementation("androidx.appcompat:appcompat:1.7.1")

    implementation("com.mikepenz:aboutlibraries-core:14.2.0")
    implementation("com.mikepenz:aboutlibraries-compose-core:14.2.0")
    implementation("com.mikepenz:aboutlibraries-compose:14.2.0")
    implementation("com.mikepenz:aboutlibraries-compose-m3:14.2.0")
    implementation("com.mikepenz:aboutlibraries:14.2.0")

}
